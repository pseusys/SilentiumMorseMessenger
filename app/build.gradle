plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'io.gitlab.arturbosch.detekt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.ekdorn.silentium"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "0.1"

        resConfigs "en", "ru"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "build_version", "Build: ${defaultConfig.versionName}"
            manifestPlaceholders = [LabelsSuffix: "_prod"]
        }

        debug {
            debuggable true
            applicationIdSuffix '.dev'
            buildConfigField "String", "AUTH_NUMBER", "\"${project.ext.has("AUTH_NUMBER") ? project.ext.get("AUTH_NUMBER") : ""}\""
            buildConfigField "String", "AUTH_CODE", "\"${project.ext.has("AUTH_CODE") ? project.ext.get("AUTH_CODE") : ""}\""
            resValue "string", "build_version", "Build: ${defaultConfig.versionName}-debug"
            manifestPlaceholders = [LabelsSuffix: "_dev"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        animationsDisabled true
    }
}

task extractInstrumentedTestParameters(dependsOn: 'connectedDebugAndroidTest') {
    if ((new File("test.properties")).exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("test.properties").newDataInputStream())
        project.ext.set("AUTH_NUMBER", properties.getProperty("auth.number", ""))
        project.ext.set("AUTH_CODE", properties.getProperty("auth.code", ""))
    }
}

task disableGoogleServicesForUnitTesting(dependsOn: 'testDebugUnitTest') {
    project.tasks.forEach({ if (it.name.contains('GoogleServices')) it.enabled = false })
}

dependencies {
    kapt 'androidx.room:room-compiler:2.4.2'
    implementation platform('com.google.firebase:firebase-bom:29.1.0')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.firebaseui:firebase-ui-auth:8.0.0'
    implementation 'androidx.room:room-runtime:2.4.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.room:room-testing:2.4.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
}
